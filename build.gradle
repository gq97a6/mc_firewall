plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '2.1.21'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.gq97a6'
version = '2.0.0'

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://nexus.scarsz.me/content/groups/public/' }
    maven { url 'https://m2.dv8tion.net/releases' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    // Kotlin
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlin:kotlin-reflect:2.1.21'

    // Minecraft
    compileOnly 'io.papermc.paper:paper-api:1.21.5-R0.1-SNAPSHOT'
    compileOnly 'com.discordsrv:discordsrv:1.28.0'

    // External libraries
    implementation 'net.dv8tion:JDA:5.5.1'
    implementation 'mysql:mysql-connector-java:8.0.33'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

tasks {
    shadowJar {
        minimize()
        archiveBaseName.set(project.name)
        archiveClassifier.set('')
    }

    build {
        dependsOn shadowJar
    }

    processResources {
        def props = [version: version]
        inputs.properties props
        filteringCharset 'UTF-8'
        filesMatching('plugin.yml') {
            expand props
        }
    }
}

kotlin {
    jvmToolchain(21)
}
